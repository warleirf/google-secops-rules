/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule winevtlog_password_spray {

  meta:
    author = "Google Cloud Security"
    description = "Detect repeated authentication failure with multiple users indicative of a password spray attack."
    rule_id = "mr_5d6c9686-992f-4caf-bf1b-2fec05925c49"
    rule_name = "MITRE ATT&CK T1110.003 RW Windows Password Spray"
    type = "alert"
    data_source = "microsoft windows events"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force: Password Spraying"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1110/003/"
    mitre_attack_version = "v13.1"
    platform = "Windows"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.event_type = "USER_LOGIN"
    $login.metadata.vendor_name = "Microsoft"
    $login.principal.hostname = $hostname
    $login.target.user.userid = $user
    $login.security_result.action = "BLOCK"

  match:
    $hostname over 30m

  outcome:
    $risk_score = max(65)
    $mitre_attack_tactic = array_distinct("Credential Access")
    $mitre_attack_technique = array_distinct("Brute Force: Password Spraying")
    $mitre_attack_technique_id = array_distinct("T1110.003")
    $event_count = count_distinct($login.metadata.id)
    $user_login_threshold = max(10)
    $target_user_distinct_count = count_distinct($user)
    $target_user_count = count($user)
    $tlp = array_distinct("amber")
    // added to populate alert graph with additional context
    // Commented out principal.hostname because it is already represented in graph as match variable. If match changes, can uncomment to add to results
    //$principal_hostname = array_distinct($login.principal.hostname)
    $principal_ip = array_distinct($login.principal.ip)
    $target_hostname = array_distinct($login.target.hostname)
    $src_hostname = array_distinct($login.src.hostname)
    $target_ip = array_distinct($login.target.ip)
    $principal_user_userid = array_distinct($login.principal.user.userid)
    $target_user_userid = array_distinct($login.target.user.userid)
    $principal_resource_name = array_distinct($login.principal.resource.name)
    $target_resource_name = array_distinct($login.target.resource.name)
    $target_url = array_distinct($login.target.url)

  condition:
    #user > 10
}
